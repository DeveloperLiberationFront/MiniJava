// Copyright (c) Mark P Jones, Portland State University
// Subject to conditions of distribution and use; see LICENSE for details
// February 3 2008 11:12 AM

/** A jacc parser for the Mini Java Compiler
 */

%package   syntax
%extends   Phase

%{
import compiler.Phase;
import compiler.Handler;
import compiler.Failure;
import compiler.Warning;
import compiler.Position;
import lexer.MjcLexer;
import syntax.MjcList;
import java.util.ArrayList;

%}
%semantic Object

// Separators:
%token <Position>       '(' ')' '{' '}' ';' ',' '.' '[' ']' ARRAY

// Operators:
%token <Position>       '>' '<' EQEQ NEQ               // relational
%token <Position>       '='                            // assignment
%token <Position>       '+' '-' '*' '/' '%'            // arithmetic
%token <Position>       '&' '|' '^' CAND COR '!'       // logic

// Keywords:
%token <Position>       BOOLEAN CLASS ELSE EXTENDS IF INT CHAR PTR NEW 
%token <Position>       RETURN STATIC SUPER THIS VOID WHILE DO
%token <Position>       ABSTRACT PUBLIC PRIVATE PROTECTED
%token <Position>       INTERFACE IMPLEMENTS

// Literals:
%token <Position>       TRUE FALSE NULL
%token <IntLiteral>     INTLIT

// Identifiers:
%token <Id>             IDENT

//Strings
%token <StringLiteral>  STRING
%token <CharLiteral>    CHARLIT

// Nonterminals:
%type  <ClassType>      class
%type  <InterfaceType>  interface
%type  <Name>           name
%type  <Type>           type extends d_type
%type  <Expression>     expr literal expr cOrExpr cAndExpr orExpr andExpr xorExpr
%type  <Expression>     eqExpr relExpr addExpr mulExpr unary primary
%type  <LeftHandSide>   lhs field
%type  <Decls>          decl decls
%type  <Args>           args args1
%type  <Formals>        formal formals formals1
%type  <VarDecls>       vardecl vardecls
%type  <Modifiers>      modifiers
%type  <Position>       pos
%type  <Statement>      stmt
%type  <MjcList.StmtList> stmts
%type  <MjcList.TypeList> implements_name implements
%type  <Block>          block body
%type  <AssignExpr>     assign 
%type  <NewExpr>        new
%type  <StatementExpr>  exprstmt

%%

program : toplevels
        ;

toplevels : toplevels class         { addClass($2); }
          | toplevels interface     { addClass($2); }
          | /* empty */             { $$ = null; }
          ;

class   : modifiers CLASS IDENT extends implements '{' decls '}'
                                { checkClassModifiers($1);
                                  checkClassDecls($7);
                                  ClassType c = new ClassType($1, $3, $4, $5.toArray(), $7);
                                  addClass(new ArrayType($1, $3, c));
                                  $$ = c; }
        ;
extends : /* empty */           { $$ = null; }
        | EXTENDS name          { $$ = new NameType($2); }
        ;

implements : /* empty */                   { $$ = new MjcList.TypeList(); }
           | IMPLEMENTS implements_name    { $$ = $2; }
           ;

implements_name : implements_name ',' name { $$ = $1.append(new NameType($3));}
                | name                     { $$ = new MjcList.TypeList(new NameType($1)); }
                ;

decls   : decls decl            { $$ = $2.link($1); }
        | /* empty */           { $$ = null; }
        ;
decl    : modifiers type vardecls ';'
                                { $$ = new FieldDecl($1, $2, VarDecls.reverse($3)); }
        | modifiers IDENT '(' formals ')' body 
                                { $$ = new MethDecl(true, $1, Type.VOID, $2, $4, $6); }
        | modifiers VOID IDENT '(' formals ')' body
                                { $$ = new MethDecl(false, $1, Type.VOID, $3, $5, $7); }
        | modifiers type IDENT '(' formals ')' body
                                { $$ = new MethDecl(false, $1, $2,   $3, $5, $7); }
        ;


interface : modifiers INTERFACE IDENT extends '{' decls '}' 
                      { checkInterfaceModifiers($1);
                        checkInterfaceDecls($6);
                        $$ = new InterfaceType($1, $3, $4, $6); }
          ;

modifiers
	: pos			{ $$ = new Modifiers($1); }
	| modifiers PUBLIC      { setPublic($1); }
	| modifiers PRIVATE     { setPrivate($1); }
	| modifiers PROTECTED   { setProtected($1); }
	| modifiers ABSTRACT    { setAbstract($1); }
	| modifiers STATIC      { setStatic($1); }
	;
pos     : /* empty */           { $$ = lexer.getPos(); }
        ;

type    : d_type                { $$ = $1; }
        | d_type ARRAY          { Id array = new Id(lexer.getPos(), $1.toString() + "[]");
                                  $$ = new NameType(new Name(array)); }
        ;
d_type  : INT                   { $$ = Type.INT; }
        | BOOLEAN               { $$ = Type.BOOLEAN; }
        | CHAR                  { $$ = Type.CHAR; }
        | PTR                   { $$ = Type.PTR; }
        | name                  { $$ = new NameType($1); }
        ;
name    : IDENT			{ $$ = new Name($1); }
        | name '.' IDENT        { $$ = new Name($1, $3); }
        ;
vardecls: vardecls ',' vardecl  { $$ = $3.link($1); }
        | vardecl
        ;
vardecl : IDENT                 { $$ = new VarDecls($1); }
        | IDENT '=' expr        { $$ = new VarDecls($1, $3); }
        ;
formals : formals1              { $$ = $1; }
        | /* empty */           { $$ = null; }
        ;
formals1: formals1 ',' formal   { $$ = $3.link($1); }
        | formal
        ;
formal  : type IDENT            { $$ = new Formals($1,$2); }
        ;
body    : /* empty */ ';'       { $$ = null; }
        | block
        ;
stmt    : /* empty */ ';'       { $$ = new Empty($1); }
        | IF '(' expr ')' stmt  { $$ = new IfThenElse($1, $3, $5, new Empty($1)); }
        | IF '(' expr ')' stmt ELSE stmt
                                { $$ = new IfThenElse($1, $3, $5, $7); }
        | WHILE '(' expr ')' stmt
                                { $$ = new While($1, $3, $5); }
        | DO stmt WHILE '(' expr ')' ';'
                                { $$ = new DoWhile($1, $5, $2); }
        | exprstmt ';'          { $$ = new ExprStmt($2, $1); }
        | RETURN ';'            { $$ = new Return($1); }
        | RETURN expr ';'       { $$ = new Return($1, $2); }
        | block
        | type pos vardecls ';' { $$ = new LocalVarDecl($2,$1, VarDecls.reverse($3));}
        ;
block   : '{' stmts '}'         { $$ = new Block($1, $2.toArray()); }
        ;
exprstmt: call
        | new
        | assign
        ;
stmts   : /* empty */           { $$ = new MjcList.StmtList(); }
        | stmts stmt            { $$ = $1.append($2); }
        ;
expr    : assign
        | cOrExpr
        ;
assign  : lhs '=' expr          { $$ = new AssignExpr($2, $1, $3); }
        ;
lhs     : name                  { $$ = new NameAccess($1); }
        | field
        ;
cOrExpr : cOrExpr COR cAndExpr  { $$ = new CondOrExpr($2, $1, $3); }
        | cAndExpr
        ;
cAndExpr: cAndExpr CAND orExpr  { $$ = new CondAndExpr($2, $1, $3); }
        | orExpr
        ;
orExpr  : orExpr '|' xorExpr    { $$ = new BitOrExpr($2, $1, $3); }
        | xorExpr
        ;
xorExpr : xorExpr '^' andExpr   { $$ = new BitXorExpr($2, $1, $3); }
        | andExpr
        ;
andExpr : andExpr '&' eqExpr    { $$ = new BitAndExpr($2, $1, $3); }
        | eqExpr
        ;
eqExpr  : eqExpr EQEQ relExpr   { $$ = new EqualExpr($2, $1, $3); }
        | eqExpr NEQ  relExpr   { $$ = new NotEqualExpr($2, $1, $3); }
        | relExpr
        ;
relExpr : relExpr '<' addExpr   { $$ = new LessThanExpr($2, $1, $3); }
        | relExpr '>' addExpr   { $$ = new GreaterThanExpr($2, $1, $3); }
        | addExpr
        ;
addExpr : addExpr '+' mulExpr   { $$ = new AddExpr($2, $1, $3); }
        | addExpr '-' mulExpr   { $$ = new SubExpr($2, $1, $3); }
        | mulExpr
        ;
mulExpr : mulExpr '*' unary     { $$ = new MulExpr($2, $1, $3); }
	| mulExpr '/' unary     { $$ = new DivExpr($2, $1, $3); }
	| mulExpr '%' unary     { $$ = new ModExpr($2, $1, $3); }
        | unary
        ;
unary   : name			{ $$ = new NameAccess($1); }
        | primary
	| '-' unary		{ $$ = new NegExpr($1, $2); }
	| '!' unary		{ $$ = new NotExpr($1, $2); }
        ;
primary : literal
        | THIS                  { $$ = new This($1); }
        | '(' expr ')'          { $$ = $2; }
        | new
        | field
        | call
        ;
literal : INTLIT
        | STRING                { $$ = $1;}
        | CHARLIT               { $$ = $1; }
        | TRUE                  { $$ = new BooleanLiteral($1, true); }
        | FALSE                 { $$ = new BooleanLiteral($1, false); }
        | NULL                  { $$ = new NullLiteral($1); }
        ;
new     : NEW name '(' args ')'   { $$ = new ConstructorInvocation($2, $4); }
        | NEW d_type '[' expr ']' { Name n = new Name(new Id(lexer.getPos(), $2.toString() + "[]"));
                                    $$ = new ConstructorInvocation(n, new Args($4, null));}
        ;
field	: primary '.' IDENT	{ $$ = new ObjectAccess($1, $3); }
        | SUPER '.' IDENT       { $$ = new SuperAccess($3); }
        | name '[' expr ']'     { NameAccess n = new NameAccess($1);
                                  $$ = new ArrayAccess(lexer.getPos(), n, $3, true); }
        | primary '[' expr ']'  { $$ = new ArrayAccess(lexer.getPos(), $1, $3, true); }
        ;
call    : name '(' args ')'     { $$ = new NameInvocation($1, $3); }
        | primary '.' IDENT '(' args ')'
                                { $$ = new ObjectInvocation($1, $3, $5); }
        | SUPER '(' args ')'
                                { $$ = new SuperInvocation(lexer.getPos(), null, $3); }        
        | SUPER '.' IDENT '(' args ')'
                                { $$ = new SuperInvocation(lexer.getPos(), $3, $5); }
        ;
args    : /* empty */           { $$ = null; }
        | args1                 { $$ = Args.reverse($1); }
        ;
args1   : args1 ',' expr        { $$ = new Args($3, $1); }
        | expr                  { $$ = new Args($1, null); }
        ;

%%
    private MjcLexer    lexer;
    private ArrayList<ClassType> classes;
    private int         used = 0;
    private boolean     parsed = false;

    public MjcParser(Handler handler, MjcLexer lexer) {
        super(handler);
        classes = new ArrayList<ClassType>();
        this.lexer = lexer;
        lexer.nextToken();
        parsed = parse();
    }

    private void yyerror(String msg) {
        report(new Failure(lexer.getPos(), msg));
    }

    private void addClass(ClassType cls) {
        classes.add(cls);
    }

    public ClassType[] getClasses() {
        return classes.toArray(new ClassType[0]);
    }
    void setPublic(Modifiers mods) {
      dontRepeat (mods, Modifiers.PUBLIC,    "public");
      cantCombine(mods, Modifiers.PRIVATE,   "private and public");
      cantCombine(mods, Modifiers.PROTECTED, "protected and public");
      mods.set(Modifiers.PUBLIC);
    }

    void setPrivate(Modifiers mods) {
      dontRepeat (mods, Modifiers.PRIVATE,   "private");
      cantCombine(mods, Modifiers.PUBLIC,    "public and private");
      cantCombine(mods, Modifiers.PROTECTED, "protected and private");
      cantCombine(mods, Modifiers.ABSTRACT,  "abstract and private");
      mods.set(Modifiers.PRIVATE);
    }

    void setProtected(Modifiers mods) {
      dontRepeat (mods, Modifiers.PROTECTED, "protected");
      cantCombine(mods, Modifiers.PUBLIC,    "public and protected");
      cantCombine(mods, Modifiers.PRIVATE,   "private and protected");
      mods.set(Modifiers.PROTECTED);
    }

    void setAbstract(Modifiers mods) {
      dontRepeat (mods, Modifiers.ABSTRACT,  "abstract");
      cantCombine(mods, Modifiers.PRIVATE,   "private and abstract");
      mods.set(Modifiers.ABSTRACT);
    }

    void setStatic(Modifiers mods) {
      dontRepeat (mods, Modifiers.STATIC,  "static");
      mods.set(Modifiers.STATIC);
    }

    void dontRepeat(Modifiers mods, int flags, String msg) {
      if (mods.includes(flags)) {
        report(new Warning(mods.getPos(),
                           "Repeated use of " + msg + " modifier"));
      }
    }

    void cantCombine(Modifiers mods, int flags, String msg) {
      if (mods.includes(flags)) {
        report(new Failure(mods.getPos(), "Cannot combine " + msg));
      }
    }

    void checkClassDecls(Decls decls) {
         for (Decls d = decls; d != null; d = d.getNext()) {
             if (d instanceof FieldDecl) {
                checkFieldModifiers(d.getMods());
             } else if (d instanceof MethDecl) {
                checkMethodModifiers(d.getMods(), ((MethDecl)d).getBody());
             } else {
                report(new Failure("Unhandled Class Decl Type"));
             }
         }
    }
    void checkInterfaceDecls(Decls decls) {
         for (Decls d = decls; d != null; d = d.getNext()) {
             if (d instanceof FieldDecl) {
                checkIfaceFieldModifiers(d.getMods());
             } else if (d instanceof MethDecl) {
                 checkIfaceMethodModifiers(d.getMods(), ((MethDecl)d).getBody());
             } else {
                report(new Failure("Unhandled Class Decl Type"));
             }
         }
    }
    void checkClassModifiers(Modifiers mods) {
      if (mods.includes(Modifiers.PUBLIC
                       |Modifiers.PRIVATE
                       |Modifiers.PROTECTED)) {
        report(new Failure(mods.getPos(), "Illegal class access modifier"));
      }
    }

    void checkInterfaceModifiers(Modifiers mods) {
      if (mods.includes(Modifiers.PUBLIC
                       |Modifiers.PRIVATE
                       |Modifiers.PROTECTED)) {
        report(new Failure(mods.getPos(), "Illegal interface access modifier"));
      }
      setPublic(mods);
      setAbstract(mods);
    }

    void checkFieldModifiers(Modifiers mods) {
      if (mods.includes(Modifiers.ABSTRACT)) {
        report(new Failure(mods.getPos(), "A field cannot be abstract"));
      }
    }

    void checkMethodModifiers(Modifiers mods, Block body) {
      if (mods.includes(Modifiers.ABSTRACT) && body!=null) {
      // not sure what I have access to here
        report(new Failure(mods.getPos(),
               "An abstract method cannot have a body"));
      }
    }

    void checkIfaceMethodModifiers(Modifiers mods, Block body) {
        if (mods.includes(Modifiers.PRIVATE) || mods.includes(Modifiers.PROTECTED)) {
            report(new Failure(mods.getPos(),
                    "An interface method cannot be protected or private."));
        }

        if (mods.includes(Modifiers.STATIC)) {
            if (body == null) {
                report(new Failure(mods.getPos(),
                        "An interface method declared static must have a body."));
            }
            if (mods.includes(Modifiers.ABSTRACT)) {
                report(new Failure(mods.getPos(),
                        "An interface method declared static cannot be abstract."));
            }
            if (mods.includes(Modifiers.PUBLIC)) {
                report(new Warning(mods.getPos(),
                        "An static interface method is implicitly public."));
            }
            if (!mods.includes(Modifiers.PUBLIC)) {
                setPublic(mods);
            }
        } else {
            if (body != null) {
                report(new Failure(mods.getPos(),
                        "An non-static interface method cannot have a body."));
            }
            if (mods.includes(Modifiers.PUBLIC) || mods.includes(Modifiers.ABSTRACT)) {
                report(new Warning(mods.getPos(),
                        "An interface method is implicitly public and abstract."));
            }
            if (!mods.includes(Modifiers.PUBLIC)) {
                setPublic(mods);
            }
            if (!mods.includes(Modifiers.ABSTRACT)) {
                setAbstract(mods);
            }
        }        
    }

    void checkIfaceFieldModifiers(Modifiers mods) {
        if (mods.includes(Modifiers.ABSTRACT)) {
            report(new Failure(mods.getPos(), "A field cannot be abstract"));
        }
        if (mods.includes(Modifiers.PRIVATE) || mods.includes(Modifiers.PROTECTED)) {
            report(new Failure(mods.getPos(),
                    "An interface field cannot be protected or private."));
        }
        if (mods.includes(Modifiers.PUBLIC) || mods.includes(Modifiers.STATIC)) {
            report(new Warning(mods.getPos(),
                    "An interface field is implicitly public and abstract."));
        }

        if (!mods.includes(Modifiers.PUBLIC)) {
            setPublic(mods);
        }
        if (!mods.includes(Modifiers.STATIC)) {
            setStatic(mods);
        }
    }
